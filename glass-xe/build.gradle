apply plugin: 'com.android.application'

apply from: file('../gradle/signing.gradle')
apply plugin: 'org.jetbrains.kotlin.android'

android {
    // todo: migrate to gdk.aar stubs instead of using old SDK
    compileSdkVersion "Google Inc.:Glass Development Kit Preview:19"
    defaultConfig {
        applicationId "com.damn.anotherglass.host"
        minSdkVersion 19
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        if (null != project.ext.signingConfig) {
            config project.ext.signingConfig
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig null != project.ext.signingConfig ? signingConfigs.config : signingConfigs.debug
        }
        debug {
            signingConfig null != project.ext.signingConfig ? signingConfigs.config : signingConfigs.debug
        }
    }

    // Later Java versions are not compatible with Glass GDK
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        checkReleaseBuilds false
    }
    namespace 'com.damn.anotherglass.glass.host'
}

kotlin {
    jvmToolchain(8)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation project(':shared')
    implementation project(':glass-shared')
    // cant use androidx due to API levels
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2"
}
